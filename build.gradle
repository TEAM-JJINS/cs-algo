plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.5'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
    id "org.sonarqube" version "6.0.1.5171"
    id 'checkstyle'
    id "io.sentry.jvm.gradle" version "5.6.0"
}

group = 'kr.co.csalgo'
version = '1.0.0-SNAPSHOT'

gradle.projectsEvaluated {
    println "Configured projects: ${rootProject.subprojects*.name}"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

jacoco {
    toolVersion = "0.8.12"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
        csv.required = true
    }
}


test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}


sonar {
    properties {
        property "sonar.projectKey", "TEAM-JJINS_cs-algo"
        property "sonar.organization", "team-jjins"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.coverage.jacoco.xmlReportPaths", "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.exclusions", "**/src/main/java/kr/co/csalgo/web/**/*"

        property "sonar.issue.ignore.multicriteria", "e1"
        property "sonar.issue.ignore.multicriteria.e1.ruleKey", "java:S00105"
        property "sonar.issue.ignore.multicriteria.e1.resourceKey", "**/MailTemplate.java"
    }
}

checkstyle {
    maxWarnings = 0
    configFile = file("${rootDir}/config/checkstyle/naver-checkstyle-rules.xml")
    configProperties = ["suppressionFile": "${rootDir}/config/checkstyle/naver-checkstyle-suppressions.xml"]
    toolVersion = "10.23.0"
}

tasks.withType(Checkstyle) {
    reports {
        xml.required = true
        html.required = false
    }
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'io.spring.dependency-management'

    group = 'kr.co.csalgo'
    version = '1.0.0-SNAPSHOT'

    java {
        sourceCompatibility = JavaVersion.VERSION_21
    }

    dependencies {
        /* Lombok */
        compileOnly 'org.projectlombok:lombok:1.18.30'
        annotationProcessor 'org.projectlombok:lombok:1.18.30'
        testCompileOnly 'org.projectlombok:lombok:1.18.30'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'

        /* spring */
        api 'org.springframework:spring-web:6.2.7'
        api 'org.springframework:spring-context:6.2.7'
        api 'org.springframework:spring-webmvc:6.2.7'
        api 'org.springframework.data:spring-data-jpa:3.4.5'
        api 'org.springframework.boot:spring-boot-starter-validation:3.4.5'

        /* jackson */
        api 'com.fasterxml.jackson.core:jackson-databind:2.18.3'
        api 'com.fasterxml.jackson.core:jackson-annotations:2.18.3'
        api 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.18.3'

        /* slf4j */
        api 'org.slf4j:slf4j-api:2.0.16'

        /* servlet */
        implementation 'jakarta.servlet:jakarta.servlet-api:6.0.0'
        implementation 'jakarta.validation:jakarta.validation-api:3.1.0'
        implementation 'jakarta.persistence:jakarta.persistence-api:3.1.0'
        implementation 'jakarta.el:jakarta.el-api:5.0.1'

        /* 공통 테스트 */
        testImplementation 'org.springframework.boot:spring-boot-starter-test:3.4.5'
    }

    test {
        useJUnitPlatform()
        testLogging {
            showStandardStreams = true
            showCauses = true
            showExceptions = true
            showStackTraces = true
            exceptionFormat = 'full'
        }
    }
}

dependencies {
    implementation project(":csalgo-common")

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'com.mysql:mysql-connector-j'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'io.sentry:sentry-spring-boot-starter-jakarta:8.12.0'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    testImplementation 'com.github.codemonstur:embedded-redis:1.4.3'
    implementation 'net.javacrumbs.shedlock:shedlock-spring:6.3.0'
    implementation 'net.javacrumbs.shedlock:shedlock-provider-redis-spring:6.3.0'
    implementation 'org.apache.lucene:lucene-core:8.11.2'
    implementation 'org.apache.lucene:lucene-analyzers-nori:8.11.2'
    implementation 'org.springframework.boot:spring-boot-starter-security'
}

tasks.named('test') {
    useJUnitPlatform()
}
